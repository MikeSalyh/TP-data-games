<?xml version="1.0" encoding="utf-8"?>

<!-- ShipOdyssey.mxml -->
<!-- Copyright (c) 2011-2012 by University of Massachusetts and contributors -->
<!-- Project information: http://srri.umass.edu/datagames/ -->
<!-- Released under the MIT License http://www.opensource.org/licenses/mit-license.php -->
<!-- Keep Calm and Chive On -->

<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   width="518" height="410" creationComplete="startUp()" currentState="StartPage"
			   pageTitle="Ship Odyssey"
			   backgroundColor="#CCCCCC"
			   backgroundColor.TreasureHunt="#294867">
	<fx:Style source="ShipOdyssey.css"/>

	<fx:Script>
		<![CDATA[
			import com.kcpt.scriptInterface.ScriptInterface;
			
			import common.MathUtilities;
			import common.ParkMiller;
			
			import mx.controls.Alert;
			import mx.core.SoundAsset;
			import mx.events.CloseEvent;
			import mx.flash.UIMovieClip;
			import mx.managers.PopUpManager;
			import mx.utils.StringUtil;
			
			import odyssey.TreasureHuntLevelWindow;
			
			import odysseyAssets.*;
			
			import spark.filters.GlowFilter;
			
			// ********** TREASURE HUNT CONSTANTS AND VARIABLES **********
			public static const kHuntLevelOne:uint = 0;	// First treasure hunt game level.
			public static const kHuntLevelTwo:uint = 1;	// Second treasure hunt game level.
			//**these former constant are now maleable due to a change to level mechanics**
[Bindable]	public static var kMaxRats:uint	= 500;	// Number of rats available per Treasure Hunt Game.
[Bindable]	public static var kMinRats:uint	= 1;	// Default minimum number of rats to send.
			public static var kRatsToSend:uint= 10;	// Default number of rats to send.
			public static var kCostPerRat:uint= 100;	// Cost per rat in dollars.
			//** **
			public static const kMaxDays:uint	= 28;	// Initial number of days given to find all treasures.
			public static const kMaxHookDrops:int = 8;	//Changing from day based game play to hook drop try based.
			public static const kCostPerHoodDrop:int = 4500; // Cost each time you drop a hook
			public static const kDaysToDropHook:uint = 1;	 // Number of days used dropping the grappling hook.
			public static const kDaysToNextTreasure:uint = 4;// Number of days to sail to next treasure.
			public static const kCostPerDay:uint=3000;	// Cost for crew and ship per day in dollars.
			public static const kInitCenter:Number = 50;// Starting center position of grappling hook.
			public static const kHookCenterPrecision:int = 2;// Number of digits past the decimal to which center is rounded.
			public static const kHookRadius:uint= 2;	// Radius of grappling hook area.
			public static const kNumTreasures:uint = 6;	// Number of treasures to find.
			public static const kTreasureID:uint= 0;	// Array position of categorical treasure ID.
			public static const kLocation:Number= 1;	// Array position of treasure location.
			public static const kValue:uint		= 2;	// Array position of treasure value.
			public static const kSearch:uint    = 3;	// Array position to point at the correct img to change.
			public static const kItem:uint		= 4;	// Array position of treasure item.
			public static const kMinLocation:Number = 0;	// Leftmost treasure location.
			public static const kMaxLocation:Number = 100;	// Rightmost treasure location.
			public static const kStdDeviationClear:Number = 10;// Standard deviation of rat results in clear water.
			public static const kStdDeviationMurky:Number = 15;// Standard deviation of rat results in murky water.
			public static const kWelcomeText:String = "Ahoy!!! Find ye treasure here. Send down some rats as scouts to see where the treasure lies. Then take yer chances and drop the hook!";
			public static const kNoRatsText:String	= "Arrr!!! Ye have lost all yer rats, but fear ye not! Try yer hand at the hook to gather ye some treasure. Bounty awaits ye!";
			public static const kMissed:String		= "Arrr!!! Ye have found no treasure here and lost a day fer yer trouble.";
			public static const kInSearchOf:String	= "In search of treasure #";
			public static const gameTitle1:String	= "Ship Odyssey";
				
			private var mStdDeviation:Number= kStdDeviationClear;	// Standard deviation of rat results.
			private var mLevelWindow:TreasureHuntLevelWindow;// The modal window for choosing a treasure hunt level.
			private var mHuntLevel:uint		= 0;			// Current hunt level.
			private var mPrevHuntLevel:uint	= 0;			// Used to track hunt level changes.
[Bindable]	private var mRatsLeft:uint		= kMaxRats;		// Rats left during current treasure hunt game.
			private var mRatsToSend:uint	= kRatsToSend;	// Rats to send at one time.
			private var mRandomNormal:ParkMiller;			// Class that creates a normal distribution.
			private var mMathUtils:MathUtilities = new MathUtilities();			// Class that uses SRRI math functions.
			private var mFirstRat:Boolean	= true;			// Is this the first rat sent looking for next treasure?
[Bindable]	private var mDaysLeft:uint		= kMaxDays;		// Days left to find treasure.
[Bindable]	private var mHookDropsLeft:int	= kMaxHookDrops;	// Number of drops left in current game.
			private var mCenterPosition:Number= kInitCenter;	// Center position of grappling hook.
			private var mMinHookRange:Number	= mCenterPosition - kHookRadius;	// Left edge of hook's grasp.
			private var mMaxHookRange:Number	= mCenterPosition + kHookRadius;	// Right edge of hook's grasp.
			private var mCurrentTreasureNum:uint = 0;		// Starting treasure in zero-based array.
			private var mTreasuresFound:uint= 0;			// Treasures found so far in current game.
			private var mTreasureValueSoFar:uint = 0;		// Cumulative value of treasures found in current game.
			private var mRatCostSoFar:uint = 0;				// Cumulative value of rat cost in current game.
			private var mShipCostSoFar:uint = 0;			// Cumulative value of ship/crew cost in current game.
			private var mProfitSoFar:int	= 0;			// Profit so far in current game.
			private var mEventNumber:uint	= 0;			// Number of current event in current game (rat(s) sent or hook dropped), begining with 1.
			private var mGameNumber:uint	= 0;			// Number of current game, begining with 1.
			private var mRatsSent:uint		= 0;			// Number of rats already sent in current game.
			private var mHooksDropped:uint	= 0;			// Number of hooks already dropped in current game.
			private var mCurrentGameComplete:Boolean = false;// Has current game been won or lost yet?
			private var mFoundTreasure:Boolean = false; 	// Has the current hook drop found this treasure?
			private var mDaysLeftTarget:uint = kMaxDays;				// used as target value of mDaysLeft for when we are tweening it (so we know what it is supposed to be)
			
			// These arrays store the details of individual treasures.
			private var mTreasure0:Array;
			private var mTreasure1:Array;
			private var mTreasure2:Array;
			private var mTreasure3:Array;
			private var mTreasure4:Array;
			private var mTreasure5:Array;

			// Array of treasures (zero-based).
			private var mTreasureArray:Array;
			
			// Array of numeric hook drop order (1st, 2nd, 3rd, etc.)
			private var mHookDropOrderArray:Array;
			
			// ********** END OF TREASURE HUNT CONSTANTS AND VARIABLES **********
			
			// Embed the start screen pirate ship graphic.
			// From http://www.rw-designer.com/cursor-download.php?id=2222 on 12/11/2010.
			// Cursor details are at: http://www.rw-designer.com/cursor-detail/2222
			//   "Published on August 14th 2007 by The Sword of the Heart."
			//   "Released under the Attribution Required (CC by) license."
			//   "Creative Commons - Attribution" see http://www.rw-designer.com/licenses
			// Converted from .eps format to .jpg.
			[Embed(source="../odysseyAssets/Pirate Ship 515 x 410.png")]
			[Bindable]
			private var mPirateShipImage:Class;
			
			// ********** TREASURE HUNT IMAGES **********
	
			// Embed the parchment image in the Treasure Hunt game.
			// Parchment image is from http://www.clker.com/clipart-15930.html on 4/4/2011.
			// From http://www.clker.com/disclaimer.html
			//   "Clker.com is owned by Rolera LLC, an Illinois Limited Liability Corporation." 
			//   "Clker and Clker.com are trademarks of Rolera LLC."
			//   "Clker.com is an online sharing service where users share free public domain" 
			//   "vector cliparts, or share public domain photos and derive vector cliparts"
			//   "from those photos using clker's online tracer."
			[Embed(source="../odysseyAssets/Parchment.png")]
			[Bindable]
			private var mParchmentImage:Class;
			
			// Embed the All Treasures Found image in the Treasure Hunt game.
			// Parchment image is from http://www.clker.com/clipart-15930.html on 4/4/2011.
			// See Disclaimer above
			// Modified by adding text in Photoshop CS5.
			[Embed(source="../odysseyAssets/All Treasures Found.png")]
			[Bindable]
			private var mAllTreasuresFoundImage:Class;
			
			// Embed the Time Up and Treasures Found image in the Treasure Hunt game.
			// Parchment image is from http://www.clker.com/clipart-15930.html on 4/4/2011.
			// See Disclaimer above
			// Modified by adding text in Seashore on Mac.
			[Embed(source="../odysseyAssets/Time Up and Treasures Found.png")]
			[Bindable]
			private var mTimeRanOutImage:Class;
			
			// Embed the Congratulations image in the Treasure Hunt game.
			// Parchment image is from http://www.clker.com/clipart-15930.html on 4/4/2011.
			// See Disclaimer above
			// Modified by adding text in Seashore on Mac.
			[Embed(source="../odysseyAssets/Congratulations.png")]
			[Bindable]
			private var mCongratulationsImage:Class;

			// Embed the NoTreasuresFound image in the Treasure Hunt game.
			// Parchment image is from http://www.clker.com/clipart-15930.html on 4/4/2011.
			// See Disclaimer above
			// Modified by adding text in Seashore on Mac.
			[Embed(source="../odysseyAssets/No Treasures Found.png")]
			[Bindable]
			private var mNoTreasuresFoundImage:Class;
			
			// Embed the new UI items for treasure hunt.
			// All components created by Tristan Warneke except the Parchment which is the same as above
			
			// Backer to Hook/Crane controls
			[Embed(source="../odysseyAssets/SO-UI2-HookDropBackerExt.png")]
			[Bindable] private var mHookDropBacker:Class;
			
			// Backer to Rat controls
			[Embed(source="../odysseyAssets/SO-UI2-DivingRatsBacker.png")]
			[Bindable] private var mRatBacker:Class;
			
			// In Search of Treasure backer
			[Embed(source="../odysseyAssets/SO-UI2-SearchingPanel.png")]
			[Bindable] private var mSearchPanel:Class;
			
			// In Search of Treasure Checkmark when you get a treasure
			[Embed(source="../odysseyAssets/SO-UI2-SearchingFound.png")]
			[Bindable] private var mSearchFound:Class;
			
			// In Search of Treasure red X when you skip a treasure
			[Embed(source="../odysseyAssets/SO-UI2-SearchingSkipped.png")]
			[Bindable] private var mSearchSkip:Class;
			
			// Status Scroll backer
			[Embed(source="../odysseyAssets/SO-UI2-StatusScroll.png")]
			[Bindable] private var mScrollImage:Class; 
			
			
			// Embed volume icons -- source: Ryan McCann
			[Embed(source="../odysseyAssets/Volume.png")]
			[Bindable]
			private var mVolumeIcon:Class;
			
			[Embed(source="../odysseyAssets/Volume_half.png")]
			[Bindable]
			private var mHalfVolumeIcon:Class;
			
			[Embed(source="../odysseyAssets/Volume_off.png")]
			[Bindable]
			private var mLowVolumeIcon:Class;
			
			// **NOTE: If you are looking for the button images, they are embedded in skin mxml files in the skins folder.
			
			// ********** END OF EMBEDDED TREASURE HUNT IMAGES **********
			
			// ********** TREASURE HUNT SOUNDS ********
			
			// Embed the cash earned sound in the Treasure Hunt game.
			// New Cash register sound by Benboncan (only modification is converted to MP3)
			// Downloaded from http://www.freesound.org/people/Benboncan/sounds/91924/ on Feb 7, 2012
			// Licensed under the attribution license (http://creativecommons.org/licenses/by/3.0/)
			[Embed(source="../odysseyAssets/91924-benboncan-till-with-bell.mp3")]
			private var CashMP3:Class;
			private var mCashSound:SoundAsset = new CashMP3() as SoundAsset;
			
			// Embed the rat squeal sound in the Treasure Hunt game.
			// The link from which the sound came no longer exists as of 10/28/2011. The larger
			// web site from which the sound came redirects to another site.
			// A similar rat sound on on the new site requires login to hear. The official web
			// site of those sounds creator lists rats sounds for a price.
			// Modified by shortening clip in Audacity on Mac.
//			[Embed("../src/assets/Rat Sound.mp3")]
//			private var RatMP3:Class;
//			private var mRatSound:SoundAsset = new RatMP3() as SoundAsset;
			
			// ********** END TREASURE HUNT SOUNDS **********

			// startUp() is called when the creation of this class is complete
			// and sets up necessary parameters for game initiation.
			private	function startUp():void	
			{
				mRandomNormal	= new ParkMiller();		// Create class that generates a normal distribution.
				
				// Create rat collection.
				ScriptInterface.NewCollectionWithAttributes(
					"Events",
					[
						"Game_Number",		// Treasure hunt game number starting with 1 since launch of application.
						"Ocean_Quality",	// Clear or murky, based on hunt level chosen.
						"Rats_Sent",		// Rats sent so far in current game.
						"Rat_Reading",		// Location estimate provided by rat.
						"Drop_Order",		// Hooks dropped so far in current game.
						"Drop_Location",	// Location of hook center at time of event.
						"Drop_Result",		// Outcome of dropping hook (None, Miss, Hit, etc.).
						"Treasure_Sought",	// Categorical treasure ID (1-based) at time of event.
						"Treasure_Location"// Exact location of treasure found at time of event
					]
				);
				// Create game collection.
				ScriptInterface.NewCollectionWithAttributes(
					"Games",
					[
						"Game_Number",		// Treasure hunt game number starting with 1 since launch of application.
						"Ocean_Quality",	// Clear or murky, based on hunt level chosen.
						"Total_Rats_Sent",	// Total number of rats sent during current game.
						"Total_Drops",		// Number of times hook was dropped during current game.
						"Treasures_Found",	// Number of treasures found during current game.
						"Journey_Time",		// Number of days used to complete treasure hunt game.
						"Profit"			// Value of (treasures found) minus (crew/ship costs and rat costs).
					]
				);
			}
			
			// switchToTreasureHunt() sets the current state to the Treasure Hunt game
			// screen and initializes the game.
			protected function switchToTreasureHunt(iEvent:MouseEvent):void
			{
				currentState='TreasureHunt';	// Switch to Treasure Hunt screen.
				
				GameLabel.text = gameTitle1;
				// Create a  game level modal dialog.
				// Listen for clicks on level buttons in dialog.
				mLevelWindow = new TreasureHuntLevelWindow();
				
				chooseHuntLevel(); // Make the user select a hunt level and return.
				
				initializeTreasureHunt();
			}
			
			// ********** TREASURE HUNT FUNCTIONS **********
			
			// initializeTreasureHunt() sets the Treasure Hunt game to initial values
			// and prepares the game for play.
			protected function initializeTreasureHunt():void
			{
				disableAllButtons(true);
				mGameNumber 			+= 1;	// Count current game.
				mEventNumber			= 0;	// Don't count events until they occur (rat(s) sent or hook dropped).	
				mRatsSent				= 0;	// Reset number of rats sent.
				mHooksDropped			= 0;	// Reset number of hooks dropped.
				mCurrentGameComplete	= false;// Game is in play.
				
				// Create the numeric hook drop order array.
				mHookDropOrderArray = new Array(	"0th", "1st", "2nd", "3rd", "4th", "5th", "6th", "7th", "8th", "9th",
													"10th", "11th", "12th", "13th", "14th", "15th", "16th", "17th", "18th", "19th",
													"20th", "21st", "22nd", "23rd", "24th", "25th", "26th", "27th", "28th", "29th");
				
				// Set up the treasure location, value, money bag image, and item.
				mTreasure0 = new Array("1st", randomLocation(), 10000, T1Search, "Brass Chalice");
				mTreasure1 = new Array("2nd", randomLocation(), 20000, T2Search, "Sword with Ebony Handle");
				mTreasure2 = new Array("3rd", randomLocation(), 30000, T3Search, "Pearl Necklace");
				mTreasure3 = new Array("4th", randomLocation(), 40000, T4Search, "Silver Goblet");
				mTreasure4 = new Array("5th", randomLocation(), 50000, T5Search, "Gold Bar");
				mTreasure5 = new Array("6th", randomLocation(), 60000, T6Search, "Diamond Ring");
				
				// Create new log array and add log/logtop to array.
				mTreasureArray = new Array(	mTreasure0, mTreasure1, mTreasure2, 
					mTreasure3, mTreasure4, mTreasure5);

				mCurrentTreasureNum 		= 0;	// Player begins looking for first treasure.
				mTreasuresFound				= 0;	// No treasures found yet.
				mTreasureValueSoFar			= 0;	// No value earned until treasures are found.
				mRatCostSoFar				= 0;	// No rats have been used yet, so no cost yet.
				mShipCostSoFar				= 0;	// No ship / crew costs until trip starts.
				mProfitSoFar				= 0;	// No profit until costs & treasures accrue.
				mFirstRat					= true;	// Next rat sent will be the first.

				// Set number of rats left to full amount.
				mRatsLeft					= kMaxRats;	
				
				mRatStepper.maximum	= kMaxRats;	//Set maximum rats-to-send to the full number of rats.
				mRatStepper.minimum	= kMinRats;	//Set minimum rats-to-send to default.
				
				// Set intial number of rats to send.
				mRatsToSend			= kRatsToSend;
				mRatStepper.value	= mRatsToSend;
				mResultsFieldLabel.text	= "";		// Clear rat results.
				
				// Move hook to center of treasure area.
				mCenterPosition				= kInitCenter;	
				mCenterPositionStepper.value= mCenterPosition;					// Set stepper to center position.
				mMinHookRange				= mCenterPosition - kHookRadius;	// Left edge of hook's grasp.
				mMaxHookRange				= mCenterPosition + kHookRadius;	// Right edge of hook's grasp.
				mRangeValueLabel.text		= mMinHookRange.toFixed(1) + " - " + mMaxHookRange.toFixed(1);
				
				// Set number of treasure sought to first.
				mInSearchOfLabel.text = (mCurrentTreasureNum + 1).toString()+"/6";
				
				// Set profit value to intial amount.
				mProfitValue.text	= formatDollars(mProfitSoFar);
				// Hide all parchment announcements until they are needed.
				mAllTreasuresFoundAlert.visible	= false;
				mTimeRanOutAlert.visible		= false;
				mCongratulationsAlert.visible	= false;
				mNoTreasuresFoundAlert.visible	= false;
				mTreasuresFoundLabel.visible	= false;
				mScrollContainer.visible = false;
				// Set days left to find treasure to full amount.
				mDaysLeft					= kMaxDays;	
				mDaysLeftTarget 			= kMaxDays;
			}
			
			// chooseHuntLevel() listens for clicks on the hunt level dialog's level buttons.
			private function chooseHuntLevel():void 
			{
				var isModal:Boolean = true;
				PopUpManager.addPopUp(mLevelWindow, this, isModal); 
				
				PopUpManager.centerPopUp(mLevelWindow);		// Center the window.
				//mLevelWindow.closeButton.visible = false;	// Hide the close button.
				
				mLevelWindow["mLevelOneButton"].addEventListener("click", firstHuntLevel);   
				mLevelWindow["mLevelTwoButton"].addEventListener("click", secondHuntLevel);   
			}
			
			// gameLevelChosen() sets the current game level to the value passed in and closes the modal dialog.
			private function huntLevelChosen(iLevel:uint):void 
			{
				mHuntLevel = iLevel; // Set game level to input value.	
				PopUpManager.removePopUp(mLevelWindow);	
				sailThenZoomIn();
			}
			
			// firstHuntLevel() sets the current hunt level to the first level.
			private function firstHuntLevel(iEvent:Event):void 
			{
				mPrevHuntLevel = mHuntLevel;		// Track game level change.
				//Set background image and standard deviation that fit with game level.
				mStdDeviation	= kStdDeviationClear;
				Water.fill = clearWater;
				Sky.backgroundFill = daylight;
				huntLevelChosen(kHuntLevelOne); // Set game level to first level.
				
				kMaxRats	= 2400;	// Number of rats available per Treasure Hunt Game.
				kMinRats	= 200;	// Default minimum number of rats to send.
				mRatsToSend	= 200;	// Default number of rats to send.
				kCostPerRat	= 0;	// Cost per rat in dollars.
				mRatsLeft = kMaxRats; 
				mRatStepper.maximum = 200;
			}
			
			// secondHuntLevel() sets the current hunt level to the second level.
			private function secondHuntLevel(iEvent:Event):void 
			{
				mPrevHuntLevel = mHuntLevel;		// Track game level change.
				// Set background image and standard deviation that fit with game level.
				//mTreasureHuntGameArea.source	= mTreasureHuntMurkyImage;
				mStdDeviation					= kStdDeviationMurky;
				Water.fill = murkyWater;
				Sky.backgroundFill = darkDay;
				cHerder.init(5,395,0x999999);
				cHerder.startClouds();
				huntLevelChosen(kHuntLevelTwo); // Set game level to second level.
			}
			
			// getOceanQuality() returns a string descibing the ocean quality based on the hunt level
			protected function getOceanQuality():String
			{
				switch (mHuntLevel)
				{
					case kHuntLevelOne:
						return "Clear";
					case kHuntLevelTwo:
						return "Murky";
					default:
						return "";
				}
			}
			
			// formatDollars() adds the dollar sign to the input value, which may be negative.
			protected function formatDollars(iDollars:int):String
			{
				// Add the $ sign, comma(s), and cents.
				var withCents:String = usdFormatter.format(iDollars);
				
				// Strip out the cents before returning the currency string.
				var splitArray:Array = withCents.split(usdFormatter.decimalSeparatorFrom);
				return splitArray[0];
			}

			// randomLocation() creates a new location on the ocean floor based on a uniform
			// distribution.
			protected function randomLocation():Number
			{
				return ((kMaxLocation - kMinLocation) * mRandomNormal.uniform()) + kMinLocation;
			}
			
			// treasuresFoundString() returns a formatted string saying how many treasures were found out
			// of the total number of treasures available.
			protected function treasuresFoundString():String {
				return "Ye found " + mTreasuresFound + " of the " + kNumTreasures + " treasures!";
			}
			
			// onClickStartHuntButton() is called when the Start button in the
			// Treasure Hunt game is pressed.
			protected function onClickStartHuntButton(iEvent:MouseEvent):void{	
				initializeTreasureHunt();
			}
			
			// ************MAIN GAME FUNCTIONS************
			// onClickSendRatsButton() is called when the Send Rats button is
			// pressed.
			protected function onClickSendRatsButton(event:MouseEvent):void{
				mEventNumber += 1;	// Sending rats counts as an event.
				
				// Calculate and display how many rats are left.
				mRatsLeft -= mRatsToSend;	
				
				// Update the cumulative cost of rats sent and profit so far.
				mRatCostSoFar		+= kCostPerRat * mRatsToSend;
				mProfitSoFar		-= kCostPerRat * mRatsToSend;
				mProfitValue.text	= formatDollars(mProfitSoFar);
				
//				mRatSound.play(); // Play rat sound.
				
				// For every rat sent...
				var readingsAdded:String = ""; //added to hopefully speed things up for large calls since I think the display call slows things down.
				for (var ratNum:uint = 0; ratNum < mRatsToSend; ++ratNum)
				{
					mRatsSent += 1;	// Increment rat counter.
					// Generate a noisy result based on the current treasure's location.
					var location:Number		= mTreasureArray[mCurrentTreasureNum][kLocation];
					var noisyLocation:Number = Math.round((mRandomNormal.standardNormal() * mStdDeviation) + location); // Display nothing after decimal point.
					// Add results of what the rats find to the results field.
					if (mFirstRat){// If first rat, dont add punctuation.
						//mResultsFieldLabel.text += noisyLocation; 
						readingsAdded += noisyLocation;
						mFirstRat = false;
					}
					else
						// All successive results should add comma delimiters.
						//mResultsFieldLabel.text += ", " + noisyLocation; // Display nothing after decimal point.
						readingsAdded += ", " + noisyLocation;
					// Send event data to TinkerPlots / Fathom.
					sendEventData("Rat", noisyLocation.toString());
				}
				mResultsFieldLabel.text += readingsAdded;
				
				// Set maximum rats-to-send to the number of rats left.
				// Adjust rat stepper to comply with any changes.
				mRatStepper.maximum	= mRatsLeft;
				if (mRatsToSend > mRatsLeft)
				{
					mRatsToSend			= mRatsLeft;	// Can't send more rats than there are!
					
					// If there are not enough rats left to send the default minimum, allow 
					// mimimum rats-to-send to drop below default minimum.
					if (mRatsToSend < mRatStepper.minimum) mRatStepper.minimum = mRatsToSend;				
					mRatStepper.value	= mRatsToSend;	// Set the stepper to reflect changes.
				}
				// Disable Send Rats button if there are no more rats left after sending.
				if (mRatsToSend == 0){
					mSendRatsButton.enabled = false;
				}
			}
			
			// onChangeCenterPosition() is called when the value of the hook's
			// center position is changed.
			protected function onChangeCenterPosition(event:Event):void
			{
				// Update the hook's range based on the center position.
				//mCenterPositionStepper.value = mMathUtils.setPrecision(mCenterPositionStepper.value, kHookCenterPrecision);
				var prevCP:int = mCenterPosition;
				mCenterPosition			= mCenterPositionStepper.value;
				mMinHookRange			= mCenterPosition - kHookRadius;	// Left edge of hook's grasp.
				mMaxHookRange			= mCenterPosition + kHookRadius;	// Right edge of hook's grasp.
				mRangeValueLabel.text	= mMinHookRange.toFixed(1) + " - " + mMaxHookRange.toFixed(1);
				if(Math.abs(prevCP - mCenterPosition)>2)
					crane.tweenTo(mCenterPosition, 1); //goto frame = mCenterPosition, tween length = 1 second
				else
					crane.runTo(mCenterPosition);
			}
			
			// onChangeRatStepper() is called when the number of rats to be sent
			// is changed.
			protected function onChangeRatStepper(event:Event):void
			{
				// Update number of rats to be sent at one time.
				mRatsToSend = mRatStepper.value;
			}
			
			// onClickDropHookButton() is called when the Drop Hook button is
			// pressed and handles all actions assiociated with attempting to
			// retrieve treasure at the specified location.
			protected function onClickDropHookButton(event:MouseEvent):void
			{
				mDropHookButton.enabled = false;
				//check to see if the crane needs to move before we can drop the hook
				//this happens if someone changes the value of the stepper and then clicks the Drop Hook button before the crane finishes moving
				if(mCenterPosition != mCenterPositionStepper.value || Math.abs(crane.getFrame()- mCenterPosition)>1){
					transitionTimer = new Timer(500, 0);
					transitionTimer.addEventListener(TimerEvent.TIMER,waitOnCraneMove);
					transitionTimer.start();
				}else{
					// Clear hint text next to pirate head.
					mEventNumber 	+= 1; // Dropping a hook counts as an event.
					mHooksDropped 	+= 1; // Increment counter of hooks dropped.
					
					// Set days left to find treasure.
					//mDaysLeft 		-= kDaysToDropHook;	
					mHookDropsLeft--;
					
					// Update the cumulative cost of using the ship and crew as well as the profit so far.
					mShipCostSoFar		+= kCostPerDay * kDaysToDropHook;
					mProfitSoFar		-= kCostPerDay * kDaysToDropHook;
					mProfitValue.text	= formatDollars(mProfitSoFar);
					
					// Show another treasure if the hook was dropped on the current location.
					var location:Number = mTreasureArray[mCurrentTreasureNum][kLocation];
					mFoundTreasure = (location >= mMinHookRange && location <= mMaxHookRange);
					dropHook();
				}
			}
			//wait and check timer routine for when the hook is moving before a drop
			private function waitOnCraneMove(e:TimerEvent):void{
				if(mCenterPosition == mCenterPositionStepper.value && Math.abs(crane.getFrame()- mCenterPosition)<1){
					transitionTimer.stop();
					transitionTimer.removeEventListener(TimerEvent.TIMER, waitOnCraneMove);
					onClickDropHookButton(new MouseEvent(MouseEvent.CLICK));
				}
			}
			
			//call the animation, disable all buttons during animation, and start a timer
			private function dropHook():void{
				disableAllButtons(false);
				crane.dropHook(mFoundTreasure, 1, 5);
				transitionTimer = new Timer(5000, 1);
				transitionTimer.addEventListener(TimerEvent.TIMER_COMPLETE, hookDropTimerFinish);
				transitionTimer.start();
			}
			//return from dropHook()::transitionTimer.start()
			protected function hookDropTimerFinish(e:TimerEvent):void{
				//does the visual and sound items that occur after the hook drop finishes playing
				//or as the item on the hook comes into view
				transitionTimer.removeEventListener(TimerEvent.TIMER_COMPLETE, hookDropTimerFinish);
				var location:Number = mTreasureArray[mCurrentTreasureNum][kLocation];
				if (mFoundTreasure)
				{
					// Add treasure value to accumulated wealth.
					mTreasureValueSoFar += mTreasureArray[mCurrentTreasureNum][kValue];
					mProfitSoFar		+= mTreasureArray[mCurrentTreasureNum][kValue];
					mProfitValue.text	= formatDollars(mProfitSoFar);
					
					mCashSound.play(); // Play cash register sound.	
					// Tell the player which treasure was found and how much it is worth.
					var valueInDollars:String = usdFormatter.format(mTreasureArray[mCurrentTreasureNum][kValue]);
					// Send event data to TinkerPlots / Fathom.
					sendEventData("Hook", "Hit");

					TreasureFoundLabel.text = "Ye found the " + mTreasureArray[mCurrentTreasureNum][kItem] + " worth " + valueInDollars 
						+ " at location " + location.toFixed(1) + "!";
					TreasureFoundAlert.visible = true;
					//update Search Panel that you found the treasure
					mTreasureArray[mCurrentTreasureNum][kSearch].source = mSearchFound;
					mTreasureArray[mCurrentTreasureNum][kSearch].visible = true;
					
					// Increment treasure number and update text indicating which treasure is sought.
					//mCurrentTreasureNum++;	
					mTreasuresFound++;
				}
				else // Treasure was not found at drop location.
				{
					enableAllButtons();
					// Send event data to TinkerPlots / Fathom.
					sendEventData("Hook", "Miss");
					// Disable the Drop Hook Button if there are not enough days left
					// to drop the hook again. Game is done.
					if (mHookDropsLeft == 0 || mDaysLeft < kDaysToDropHook)
					{
						// Player ran out of time. Disable treasure hunt controls.
						disableAllButtons(false);
						gameFinish();
					}
				}
			}
			
			protected function AlertOnSailOn(event:CloseEvent):void{
				if(event.detail == Alert.YES){
					onClickSailOnButton(new MouseEvent(MouseEvent.CLICK));
				}
				//else do nothing
			}
			protected function AlertOnExit(event:CloseEvent):void{
				if(event.detail == Alert.YES){
					onClickExitTreasureHunt(new MouseEvent(MouseEvent.CLICK));
				}
			}
			
			// onClickExitTreasureHunt() sets the current state to the Treasure Hunt game
			// screen and initializes the game.
			protected function onClickExitTreasureHunt(iEvent:MouseEvent):void
			{
				// If player pressed exit before completing game, send game data to TinkerPlots / Fathom.
				if (!mCurrentGameComplete){
					sendGameData();	
				}
				//clean up everything so it is back in initial state for start of next game
				T1Search.visible = false;
				T2Search.visible = false;
				T3Search.visible = false;
				T4Search.visible = false;
				T5Search.visible = false;
				T6Search.visible = false;
				mExitFinishButton.visible = false; 
				mScrollContainer.visible = false;
				crane.visible = false;
				CraneHandle.visible = false;
				
				sailingMovie.visible = true;
				sailingMovie.reset();
				mCurrentGameComplete = true;// Game is now complete.	
				currentState='StartPage';	// Return to start screen.
			}

			// onClickSailOnButton() makes the ship set sail for the next treasure.
			protected function onClickSailOnButton(iEvent:MouseEvent):void
			{
				disableAllButtons(true);
				mScrollContainer.visible = false;
				mTreasureArray[mCurrentTreasureNum][kSearch].source = mSearchSkip;
				mTreasureArray[mCurrentTreasureNum][kSearch].visible = true;
				runSailOnAnim();
				mResultsFieldLabel.text	= "";	// Clear rat results.
				mFirstRat				= true;	// Recount rats used per treasure.		
				// End game if there are no treasures left after currently sought treasure.
				if ((mCurrentTreasureNum + 1) >= kNumTreasures) // mCurrentTreasureNum is zero-based.
				{	
					// Clear text saying which treasure is sought.
					mInSearchOfLabel.text = ""; 
					// If game isn't already over, send game data to TinkerPlots / Fathom.
					if (mCurrentGameComplete == false)
						sendGameData();
					// Tell the player the game is over.
					mCurrentGameComplete = true;// Game is complete.
					gameFinish();
				}
				else // There are still treasures left after current treasure, so sail on!
				{
					if (mDaysLeft <= kDaysToNextTreasure){
						updateProfitAfterSail();
					}
				}
			}
			
			private function updateProfitAfterSail():void{
				//update the cumulative cost of using the ship and crew as well as the profit so far
				mCurrentTreasureNum++; // Increment current treasure counter.
				mInSearchOfLabel.text = (mCurrentTreasureNum + 1).toString()+"/6"; 
				mShipCostSoFar += kCostPerDay * kDaysToNextTreasure;
				mProfitSoFar -= kCostPerDay * kDaysToNextTreasure;
				mProfitValue.text = formatDollars(mProfitSoFar);
			}
			
			private function gameFinish():void{
				// Tell the player the game is over.
				if(mHookDropsLeft == 0 || mDaysLeft == 0){
					mTimeRanOutAlert.visible = true;
					mTreasuresFoundLabel.text		= treasuresFoundString();
					mTreasuresFoundLabel.visible	= true;
				}
				else if (mTreasuresFound) // At least one treasure was found.
				{
					mCongratulationsAlert.visible	= true;
					mTreasuresFoundLabel.text		= treasuresFoundString();
					mTreasuresFoundLabel.visible	= true;
				}else{ // No treasures were found.
					mNoTreasuresFoundAlert.visible	= true;
				}
				// display exit button
				mExitFinishButton.visible = true;
				// Clear text saying which treasure is sought.
				mInSearchOfLabel.text = ""; 
				// If game didn't already end when hook dropped, send game data to TinkerPlots / Fathom.
				if (mCurrentGameComplete == false)
					sendGameData();
				mCurrentGameComplete = true;// Game is complete.
			}
			
			private function onClickLog(e:Event):void{
				//update status items
				mRatsRemaining.text = mRatsLeft.toString() + " of " + kMaxRats.toString();
				mDaysSpent.text = (kMaxDays-mDaysLeft).toString();// + " of " +kMaxDays.toString();
				mTrsrsFound.text = mTreasuresFound.toString() + " of " + kNumTreasures.toString();
				mStartingAmt.text = formatDollars(100000);
				mTreasureProfit.text = formatDollars(mTreasureValueSoFar);
				mRatCost.text = formatDollars(mRatCostSoFar);
				mShipCost.text = formatDollars(mShipCostSoFar);
				mProfitValue.text = formatDollars(100000 + mProfitSoFar);
				//make scroll visible and disable all game buttons not on scroll
				mScrollContainer.visible = true;
				disableAllButtons(false);
			}
			
			private function onClickCloseLog(e:Event):void{
				//hiding scroll automatically disables all buttons on it
				mScrollContainer.visible = false;
				enableAllButtons();
			}
			
			private function sendGameData():void{
				ScriptInterface.AddCaseToCollectionWithValues(
					"Games",
					[
						mGameNumber,		 // Treasure hunt game number starting with 1 since launch of application.
						getOceanQuality(),	 // Clear or murky, based on hunt level chosen.
						mRatsSent,			 // Total number of rats sent during current game.
						mHooksDropped,		 // Number of times hook was dropped during current game.
						mTreasuresFound, 	 // Number of treasures found during current game.
						kMaxDays - mDaysLeft,// Number of days used to complete treasure hunt game.
						mProfitSoFar		 // Value of (treasures found) minus (crew/ship costs and rat costs).
					]
				);
			}
			private function sendEventData(type:String, val:String):void{
				var uniqueTreasureID:String = (mGameNumber+"-"+mTreasureArray[mCurrentTreasureNum][kTreasureID]);
				if(type=="Hook"){
					//deprecated and removed due to little usefulness and getting in the way of Rat Readings
				}else if (type=="Rat"){
					ScriptInterface.AddCaseToCollectionWithValues(
						"Events", 
						[
							mGameNumber,			// Treasure hunt game number starting with 1 since launch of application.
							getOceanQuality(),		// Clear or murky, based on hunt level chosen.
							mRatsSent,				// Rats sent so far in current game.
							val,					// Location estimate provided by rat.
							"",						// Hooks dropped so far in current game. Blank for rat events.
							"",						// Location of hook center at time of event.
							"",		 				// Outcome of dropping hook. Blank for rat events.
							uniqueTreasureID,		// Categorical treasure ID (1-based) at time of event.
							""						// Exact location of treasure found at time of event
						]	
					);
				}
			}
			
			//group function calls for state changes for different events
			private function runSailOnAnim():void{
				disableAllButtons(false);
				transitionTimer = new Timer(2000, 1); //timer is in milliseconds
				transitionTimer.addEventListener(TimerEvent.TIMER_COMPLETE, zoomOutAndSail);
				crane.tweenTo(100, 2);//turn to frame 100 in 2 secs
				transitionTimer.start();
			}
			private function sailThenZoomIn():void{
				cHerder.setCloudSpeed(-.5);
				transitionTimer = new Timer(4000, 1);
				transitionTimer.addEventListener(TimerEvent.TIMER_COMPLETE, readyToZoomIn);
				transitionTimer.start();
			}
			
			//called by the continue button after showing the user they got a treasure
			private function continueAfterFindingTreasure(e:Event):void{
				TreasureFoundAlert.visible = false;
				mInSearchOfLabel.text = (mCurrentTreasureNum + 1).toString()+"/6"; 
				mResultsFieldLabel.text	= "";	// Clear rat results.
				
				mFirstRat				= true;	// Recount rats used per treasure.	
				if (mCurrentTreasureNum+1 == kNumTreasures)
				{	// Player found final treasure. Disable treasure hunt controls.
					mInSearchOfLabel.text = "";
					//disableAllButtons(false);
					gameFinish();
				}
				else	// Take time to sail to next treasure.
				{						
					runSailOnAnim();
				}
			}
			//timer response functions for running the game (will inline them near where they go as it makes sense)
			//(decouples visual with state and play so a broken animation doesn't stop play)
			private function zoomOutAndSail(e:TimerEvent):void{
				cHerder.setCloudSpeed(-.5);
				mCenterPositionStepper.value = 100;
				onChangeCenterPosition(new Event("Stepper"));	
				//crane is stored and ready to zoom out
				transitionTimer.removeEventListener(TimerEvent.TIMER_COMPLETE, zoomOutAndSail);
				transitionTimer = new Timer(6000, 1); //timer is in milliseconds
				transitionTimer.addEventListener(TimerEvent.TIMER_COMPLETE, readyToZoomIn);
				//spin watch hand and click down days
				watchMovie.tweenHands(10,6);//watch animation is (speed, timeInSeconds)
//				dayTickOffClockTimer = new Timer(1000);
//				dayTickOffClockTimer.addEventListener(TimerEvent.TIMER, dayTick);
//				dayTickOffClockTimer.start();
				
//				if(kDaysToNextTreasure >= mDaysLeft)
//					mDaysLeftTarget = 0;
//				else 
//					mDaysLeftTarget = mDaysLeft - kDaysToNextTreasure;
				
				sailingMovie.visible = true;
				crane.visible = false;
				sailingMovie.doZoomOut();
				transitionTimer.start()
			}

			private function readyToZoomIn(e:TimerEvent):void{
				transitionTimer.removeEventListener(TimerEvent.TIMER_COMPLETE, readyToZoomIn);
				if(mCurrentGameComplete || mDaysLeftTarget == 0 || mCurrentTreasureNum == kNumTreasures){
					//finish and show end screen
					mInSearchOfLabel.text = "";
					gameFinish();
				}else{
					//zoom back in
					cHerder.setCloudSpeed(1.0);
					transitionTimer = new Timer(4500, 1);
					transitionTimer.addEventListener(TimerEvent.TIMER_COMPLETE, zoomInTimerFinish);
					sailingMovie.doZoomIn();
					transitionTimer.start()
				}
			}
			private function zoomInTimerFinish(e:TimerEvent):void{
				crane.visible = true;
				crane.runTo(mCenterPosition);
				sailingMovie.visible = false;
				enableAllButtons();
			}
			//currently unused since there are no rat drop sounds or anims
			private function ratDropTimerFinish(e:TimerEvent):void{
				
			}
			//deprecated due to using hook drops instead of days
//			private function dayTick(e:TimerEvent):void{
//				var repeat:int = dayTickOffClockTimer.currentCount;
//				if(repeat > 2 && repeat <(3+kDaysToNextTreasure)){
//					mDaysLeft = Math.max(0, mDaysLeft - 1);
//					if (mDaysLeft > 1000) mDaysLeft = 0; // in case the uint flips to highest value possible
//					if(repeat == (2+kDaysToNextTreasure)){
//						updateProfitAfterSail();
//						dayTickOffClockTimer.stop();
//						dayTickOffClockTimer.removeEventListener(TimerEvent.TIMER, dayTick);
//					}
//				}
//			}
			//button control methods
			private function disableAllButtons(incExit:Boolean):void{
				logBtn.enabled = false;
				mDropHookButton.enabled = false;
				mCenterPositionStepper.enabled = false;
				mRatStepper.enabled = false;
				mSendRatsButton.enabled = false;
				CraneHandle.visible = false;
			}	
			private function enableAllButtons():void{
				logBtn.enabled = true;
				mDropHookButton.enabled = true;
				mCenterPositionStepper.enabled = true;
				mRatStepper.enabled = true;
				mSendRatsButton.enabled = true;
				CraneHandle.visible = true;
			}
			
			//********new visual items and visual only code **********
			private var crane:MovieClip;
			private var sailingMovie:MovieClip;
			private var cHerder:MovieClip;
			private var watchMovie:MovieClip;
			private var skyAreaMask:Shape;
			private var sailMask:Shape;
			private var craneMask:Shape;
			private var transitionTimer:Timer;
			private var dayTickOffClockTimer:Timer;
			
			//ties the crane to MXML loader and sets up the mask
			private function initCrane():void{
				crane = CraneLoader.content as MovieClip;
				crane.visible = false;
				craneMask = new Shape();
				craneMask.graphics.beginFill(0x000000);
				craneMask.graphics.drawRect(0,0,395,286);
				uic.addChild(craneMask);//have to add masks to display list or else RemovePopUp will hide them and the object they are masking
				crane.mask = craneMask;
				//crane.runTo(mCenterPosition);
			}
			//loads the swf, sets colors, and starts the clouds
			private function initClouds():void{
				cHerder = CloudLoader.content as MovieClip;
				skyAreaMask = new Shape();
				skyAreaMask.graphics.beginFill(0x000000);
				skyAreaMask.graphics.drawRect(0,0,395,186);
				uic.addChild(skyAreaMask);//have to add masks to display list or else RemovePopUp will hide them and the object they are masking
				cHerder.mask = skyAreaMask;
				cHerder.init(5,395,0xDDDDDD);
				cHerder.startClouds();
			}
			//ties boat swf loader to code
			private function initBoat():void{
				//load boat
				sailingMovie = BoatLoader.content as MovieClip;
				//set zoom variables
				sailingMovie.zoomTime = 48;
				sailingMovie.finalX = -26;
				sailingMovie.finalY = -122;
				//set mask so zoom doesn't go outside box
				sailMask = new Shape();
				sailMask.graphics.beginFill(0x000000);
				sailMask.graphics.drawRect(0,0,395,286);
				uic.addChild(sailMask);//have to add masks to display list or else RemovePopUp will hide them and the object they are masking
				sailingMovie.mask = sailMask;
				//start sailing
				sailingMovie.ToyBoat.startSail();
			}
			// tie PocketWatch.swf to the movieclip
			private function initWatch():void{
				watchMovie = WatchLoader.content as MovieClip;
			}
			
			//********** Title Bar Volume Control*********
			private function updateVolume(vol:Number):void
			{
				//cannot do: SoundMixer.soundTransform.volume = vol/100;
				var st:SoundTransform = SoundMixer.soundTransform;
				st.volume = vol/100; //changes range [0-100] (slider position) to [0-1] (volume level)
				SoundMixer.soundTransform = st;
				
				//adjust the volume level icon
				if(vol < 33){
					mVolumeImage.source = mLowVolumeIcon;
				} else if(vol > 66) {
					mVolumeImage.source = mVolumeIcon;
				} else {
					mVolumeImage.source = mHalfVolumeIcon;
				}
			}
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="StartPage"/>
		<s:State name="TreasureHunt"/>
	</s:states>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		<mx:CurrencyFormatter id="usdFormatter"				precision="2" 
							  currencySymbol="$"			decimalSeparatorFrom="."
							  decimalSeparatorTo="."		useNegativeSign="true" 
							  useThousandsSeparator="true"	alignSymbol="left"/>
		<s:LinearGradient id="murkyWater" rotation="90">
			<s:GradientEntry color="0x796f5a" ratio=".33" alpha=".75"/>
			<s:GradientEntry color="0x645a45" ratio=".66" alpha=".75"/>
		</s:LinearGradient>
		<s:LinearGradient id="clearWater" rotation="90">
			<s:GradientEntry color="0x076FA3" ratio=".33" alpha=".6"/>
			<s:GradientEntry color="0x025987" ratio=".66" alpha=".75"/>
		</s:LinearGradient>
		<s:LinearGradient id="daylight" rotation="90">
			<s:GradientEntry color="0x3BCFF3" ratio=".0" alpha=".75"/>
			<s:GradientEntry color="0xBFEFFB" ratio=".66" alpha=".75"/>
		</s:LinearGradient>
		<s:LinearGradient id="darkDay" rotation="90">
			<s:GradientEntry color="0x002358" ratio=".33" alpha=".75"/>
			<s:GradientEntry color="0x77787E" ratio=".66" alpha=".75"/>
		</s:LinearGradient>
		<s:SolidColorStroke id="separator" color="0x000000" weight="1"/>
	</fx:Declarations>

	<!--Opening screen -->
	<mx:Image	id="mStartPageBackground" x="0" y="0" source="{mPirateShipImage}" includeIn="StartPage"/>
	<s:RichText	id="mGameTitle" x="10" y="40" text="Ship Odyssey" fontFamily="Georgia" fontSize="40" color="#908F17" fontWeight="normal" fontStyle="italic" includeIn="StartPage"/>
	<s:Group	x="160" y="152" width="200" height="48" includeIn="StartPage">
		<s:Rect		id="mGroupRect" x="0" y="0" radiusX="4" radiusY="4" height="100%" width="100%">
			<s:stroke>
				<s:SolidColorStroke weight="1" scaleMode="normal" color="0xFFFFFF" alpha=".75"/>
			</s:stroke>
			<s:fill> <s:SolidColor color="0xB4B35F" alpha=".75"/> </s:fill>
		</s:Rect>        
		<s:Button	id="mTreasureHuntButton" x="10" y="10" label="Treasure Hunt" fontSize="24" fontStyle="italic" fontFamily="Georgia" height="28" click="switchToTreasureHunt(event)"/>
	</s:Group>

	<!-- Animated Ship and Crane area -->
		<s:Group	id="mGameArea" includeIn="TreasureHunt" x="120" y="121" width="395" height="286">
			<s:BorderContainer id="Sky" width="395" height="186" depth="1" backgroundFill="{daylight}" borderVisible="false">
				<s:SWFLoader id="CloudLoader" source="odysseyAssets/cloudHerder.swf" depth="2" complete="initClouds()" includeIn="TreasureHunt"/>
			</s:BorderContainer>	
			<s:Rect id="Water" fill="{clearWater}" includeIn="TreasureHunt" x="0" y="186" width="395" height="100" depth="3" />
			<s:SWFLoader id="CraneLoader" x="0" y="0" width="395" height="286" source="odysseyAssets/CraneAnimation.swf" depth="4" complete="initCrane()" includeIn="TreasureHunt"/>
			<s:SWFLoader id="BoatLoader" x="0" y="0" width="395" height="286" source="odysseyAssets/BoatSailAnim.swf" depth="5" complete="initBoat()" includeIn="TreasureHunt"/>
			<s:HSlider id="CraneHandle" includeIn="TreasureHunt" x="149" y="144" width="100" height="20" alpha="0" buttonMode="true" change="mCenterPositionStepper.value=CraneHandle.value;onChangeCenterPosition(event);"
					   liveDragging="true" depth="6" maximum="100" minimum="0"/>
		</s:Group>
		<mx:UIComponent id="uic" x="120" y="121"/> <!--Note: this is used to place the animation masks and must be the same X and Y as 'mGameArea'-->

	<!-- Scroll status items-->
		<mx:Canvas id="mScrollContainer" includeIn="TreasureHunt" x="123" y="116">
			<s:Image id="mStatusScroll" includeIn="TreasureHunt" source="{mScrollImage}" />
			<s:Label id="mRatsRemaining" includeIn="TreasureHunt" x="161" y="39" width="71"
					 color="#433211" fontFamily="Verdana" text="500 of 500" textAlign="left"/>
			<s:Label id="mDaysSpent" includeIn="TreasureHunt" x="232" y="60" width="71"
					 color="#433211" fontFamily="Verdana" text="0 of 28" textAlign="left"/>
			<s:Label id="mTrsrsFound" includeIn="TreasureHunt" x="174" y="82" width="71"
					 color="#433211" fontFamily="Verdana" text="0 of 6" textAlign="left"/>
			<s:Label id="mStartingAmt" includeIn="TreasureHunt" x="231" y="125" width="71"
					 color="#433211" fontFamily="Verdana" text="$100,000" textAlign="right"/>
			<s:Label id="mTreasureProfit" includeIn="TreasureHunt" x="231" y="147" width="71"
					 color="#433211" fontFamily="Verdana" text="$600,000" textAlign="right"/>
			<s:Label id="mRatCost" includeIn="TreasureHunt" x="231" y="168" width="71"
					 color="#C30808" fontFamily="Verdana" text="$700,000" textAlign="right"/>
			<s:Label id="mShipCost" includeIn="TreasureHunt" x="231" y="189" width="71"
					 color="#C30808" fontFamily="Verdana" text="$800,000" textAlign="right"/>
			<s:Label id="mProfitValue" includeIn="TreasureHunt" x="231" y="212" width="71"
					 color="#433211" fontFamily="Verdana" text="$900,000" textAlign="right"/>
			<s:Button id="closeLogBtn" includeIn="TreasureHunt" x="293" y="21"
					  click="onClickCloseLog(event)"/>
			<s:Button id="mExitHuntButton" includeIn="TreasureHunt" x="190" y="235" width="90" height="18" label="Exit Game" chromeColor="#D1B38B" click="Alert.show('Are you sure you would like to exit the game?','Exit Game',3,this,AlertOnExit);" color="#000000" enabled="true" fontFamily="Georgia" fontSize="14" fontStyle="italic"/>
			<s:Button id="mSailOnButton" includeIn="TreasureHunt" x="110" y="235" width="75" height="18" label="Sail On" chromeColor="#D1B38B" click="Alert.show('Are you sure you would like to skip this treasure and sail on?','Sail On to Next Treasure',3,this,AlertOnSailOn);" color="#000000" fontFamily="Georgia" fontSize="14" fontStyle="italic"/>
		</mx:Canvas>
	
	<!-- Popup Alert items -->
		
		<mx:Image	id="mAllTreasuresFoundAlert" includeIn="TreasureHunt" x="170" y="119" source="{mAllTreasuresFoundImage}" depth="6"/>
		<mx:Image	id="mTimeRanOutAlert" includeIn="TreasureHunt" x="170" y="119" source="{mTimeRanOutImage}" depth="6"/>
		<mx:Image	id="mCongratulationsAlert" includeIn="TreasureHunt" x="170" y="119" source="{mCongratulationsImage}" depth="6"/>
		<s:Label	id="mTreasuresFoundLabel" includeIn="TreasureHunt" x="228" y="245" text="Ye found 3 of the 6 treasures!" fontFamily="Georgia" fontStyle="italic" height="53" width="169" fontSize="22" textAlign="center" color="#000000" depth="7" visible="false"/>
		<mx:Image	id="mNoTreasuresFoundAlert" includeIn="TreasureHunt" x="170" y="119" source="{mNoTreasuresFoundImage}" depth="6"/>
		<s:Button id="mExitFinishButton" visible="false" depth="7" includeIn="TreasureHunt" x="270" y="335" width="90" height="18" label="Exit Game" chromeColor="#D1B38B" click="onClickExitTreasureHunt(event)" color="#000000" enabled="true" fontFamily="Georgia" fontSize="14" fontStyle="italic"/>
		<s:Group id="TreasureFoundAlert" visible="false" depth="6">
			<s:Image	id="TreasureFoundAlertImage" includeIn="TreasureHunt" x="170" y="119" source="{mParchmentImage}" depth="6"/>
			<s:Label	id="TreasureFoundLabel" includeIn="TreasureHunt" x="188" y="173" fontFamily="Georgia" fontStyle="italic" height="120" width="249" fontSize="22" textAlign="center" color="#000000" depth="7"/>
			<s:Button	id="ContinueOn" includeIn="TreasureHunt" x="250" y="300" width="125" height="18" label="Continue" chromeColor="#D1B38B" color="#000000"
					  click="continueAfterFindingTreasure(event)" depth="7" fontFamily="Georgia" fontSize="14" fontStyle="italic"/>
		</s:Group>
	<!-- Edge Controls and displays -->
		<s:Button 	id="logBtn" includeIn="TreasureHunt" x="16" y="209" click="onClickLog(event)"/>
		<s:Group id="SearchPanel" x="3" includeIn="TreasureHunt">
			<s:Image id="mInSearchBacker" includeIn="TreasureHunt" x="0" y="124" source="{mSearchPanel}"/>
				<s:Image id="T1Search" includeIn="TreasureHunt" x="9" y="170" source="" visible="false"/>
				<s:Image id="T2Search" includeIn="TreasureHunt" x="26" y="170" source="" visible="false"/>
				<s:Image id="T3Search" includeIn="TreasureHunt" x="43" y="170" source="" visible="false"/>
				<s:Image id="T4Search" includeIn="TreasureHunt" x="61" y="170" source="" visible="false"/>
				<s:Image id="T5Search" includeIn="TreasureHunt" x="79" y="170" source="" visible="false"/>
				<s:Image id="T6Search" includeIn="TreasureHunt" x="96" y="170" source="" visible="false"/>
			<s:Label 	id="mInSearchOfLabel" includeIn="TreasureHunt" x="80" y="156" text="#/6" backgroundAlpha="0.0" fontFamily="Impact" fontStyle="normal" fontSize="16" color="#294867"/>
		</s:Group>
		<s:SWFLoader id="WatchLoader" includeIn="TreasureHunt" x="9" y="287" complete="initWatch()"
					 source="odysseyAssets/PocketWatch.swf"/>
	<!-- Hook controls -->
		<s:Group id="mControlContainer" includeIn="TreasureHunt" x="273" y="31" width="240" height="76">
			<s:Image 		id="mHookBacker" x="0" y="0" source="{mHookDropBacker}"/>
			<s:Label		id="mHookLabel" x="41" y="9" text="Grappling Hook" color="#000000" fontSize="16" fontFamily="Georgia" fontWeight="bold" fontStyle="normal"/>
			<s:Label		id="mRangeLabel" x="29" y="59" text="Range:" fontFamily="Georgia" fontWeight="bold"/>
			<s:Label 		id="mRangeValueLabel" x="78" y="59" width="80" color="#9B1D79" fontFamily="Georgia" fontWeight="bold"/>
			<s:Button 		id="mDropHookButton" x="81" y="30" label="Drop Hook" width="80" height="23" fontSize="12" click="onClickDropHookButton(event)"/>
			<s:NumericStepper id="mCenterPositionStepper" x="18" y="30" width="57" minimum="0" maximum="100" stepSize="1" snapInterval="0.1" change="onChangeCenterPosition(event)" contentBackgroundColor="#5081B4"/>
			<s:Label id="mDaysRemainingValue" includeIn="TreasureHunt" x="162" y="31" width="78" color="#1A1A1A" fontSize="32"
					 fontStyle="normal" fontWeight="bold" text="{mHookDropsLeft}" textAlign="center"/>
		</s:Group>		
	<!--Rat controls -->
		<s:Group id="RatControls" includeIn="TreasureHunt" x="51" y="33" width="215" height="85">
			<s:Image id="mDivingRatBacker" x="-48" y="1" width="263" height="81"
					 source="{mRatBacker}"/>
			<s:Label 	id="mRatControlLabel" color="#000000" fontFamily="Georgia" fontSize="16" fontStyle="normal" fontWeight="bold" text="Diving Rats" x="58" y="5" includeIn="TreasureHunt"/>
			<s:NumericStepper id="mRatStepper" width="50" height="23" change="onChangeRatStepper(event)" contentBackgroundColor="#5081B4" maximum="{kMaxRats}" minimum="{kMinRats}" stepSize="1" x="34" y="28" includeIn="TreasureHunt"/>
			<s:Label id="mDiveResultsLabel" includeIn="TreasureHunt" x="-2" y="59.5" width="99" height="19" fontFamily="Georgia" text="Rat Readings:" textAlign="center"/>
			<s:BorderContainer x="88" y="53" width="111" height="25" includeIn="TreasureHunt">
				<s:Label 	id="mResultsFieldLabel" x="2" y="2" height="19" width="106" color="#9B1D79" lineBreak="explicit" verticalAlign="middle" fontWeight="bold" fontFamily="Georgia" textAlign="right"/>
			</s:BorderContainer>
			<s:Button id="mSendRatsButton" height="23" label="Send Rats" click="onClickSendRatsButton(event)" x="88" y="28" includeIn="TreasureHunt"/>	
		</s:Group>
	<!--Game Header Bar-->
	<s:BorderContainer id="StatusBar" x="3" y="0" width="{this.width - 6}" height="30" cornerRadius="3" depth="10">
		<s:backgroundFill>
			<s:LinearGradient rotation="90">
				<s:GradientEntry color="0xf0f0f0"/>
				<s:GradientEntry color="0xd8d8d8"/>
			</s:LinearGradient>
		</s:backgroundFill>
		<s:Label id="GameVersion" x="7" y="10" width="163" chromeColor="#294867" color="#294867" fontFamily="Impact" fontSize="12" fontStyle="normal" text="v.2012.04.09(FL)"/> 
		<s:Label id="GameLabel" x="163" y="7" width="163" chromeColor="#294867" color="#294867" fontFamily="Impact" fontSize="18" fontStyle="normal" text="Ship Odyssey" textAlign="center" verticalAlign="top"/>
		
		<s:Image id="mVolumeImage" x="390" y="6" source="{mVolumeIcon}"/>
		<s:HSlider id="mVolumeSlider" x="415" y="9" width="80" change="updateVolume(mVolumeSlider.value)" dataTipPrecision="0" maximum="100" value="100"/>
	</s:BorderContainer>
</s:Application>
